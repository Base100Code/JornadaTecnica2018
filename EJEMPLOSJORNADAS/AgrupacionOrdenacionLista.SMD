REPOSITORY stock
CONSTANTS  begin
	SKIN_LIST_ITEM_HEADER = 1
	SKIN_LIST_ITEM_SELECTED_ROW = 2
	SKIN_LIST_ITEM_UNSELECTED_ROW = 3
	SKIN_LIST_ITEM_SCROLL_BAR = 4
	SKIN_LIST_ITEM_SEARCH_BAR = 5
	SKIN_LIST_PART_BITMAP = 1
	SKIN_LIST_PART_BORDER = 2
	SKIN_LIST_PART_TEXT = 3
	SKIN_LIST_STATUS_FOCUSED = 1
	SKIN_LIST_STATUS_NORMAL = 2
	SKIN_LIST_STATUS_DOWN = 3
end
CLASSES BEGIN
	frm is Form begin
		objects begin
			grupo1 as Integer
			IdEtiquetasSql as Integer
			gNumRows as Integer
			InstruSelect as Char
			gFuente as Char
			gFuenteSql as Char
			gNumCol as Smallint default 13
		end
		INTERFACE
			POSITION 0 0 854 456
			LABEL "Groups and Agregates"
			RESIZING
			MAXIMIZE
			MINIMIZE
			SYSMENU
		BEGIN
			CONTROL ctr_caja AS BOX
				POSITION 0 0 838 417
				FOREGROUND RGB 0 0 0
				BACKGROUND COLOR white
				ATTACH ALL
				NOLABEL
				NOBORDER
			BEGIN
				CONTROL AS TAB
					POSITION 7 10 817 384
					ATTACH ALL
					BORDER DOUBLE
					NUMBER 1
						TAB LABEL "ShowMultiColumnGroupDlg / ShowMultiColumnSortDlg"
				BEGIN
					CONTROL lstStrings AS LIST
						POSITION 10 8 649 316
						ATTACH ALL
						LABEL "State|Customer  |Order date |Pay date |Status |Nº line |Cod. Item |Description|Quantity|Discount|Price"
						BORDER DOUBLE DOWN
						ICON FILE "_MBSTDBMP_"
						LISTTYPE LISTCOLS 11
						SELECTTYPE SIMPLE
						SCROLL VERTICAL HORIZONTAL
					CONTROL AS BUTTON
						POSITION 671 31 134 34
						ATTACH TOP RIGHT
						LABEL "Groups and Aggregates"
						COMMAND CmdAgruparAgregados
					CONTROL AS BUTTON
						POSITION 671 70 134 34
						ATTACH TOP RIGHT
						LABEL "Classic View"
						COMMAND CmdVistaClasicaString
					CONTROL AS BUTTON
						POSITION 671 109 134 34
						ATTACH TOP RIGHT
						LABEL "Order"
						COMMAND CmdOrdenarColString
					CONTROL txt_help2 AS TEXT
						POSITION 15 330 681 29
						FOREGROUND COLOR white
						BACKGROUND RGB 255 177 100
						ATTACH LEFT RIGHT BOTTOM
						PAGE 2
						BORDER ETCHED
						DATATYPE CHAR
					CONTROL AS BUTTON
						POSITION 671 166 134 34
						ATTACH TOP RIGHT
						LABEL "Non interactive groups"
						COMMAND CmdAgruparAgregadosString
					CONTROL AS BAR
						POSITION 0 328 813 31
						ATTACH LEFT RIGHT BOTTOM
						BORDER ETCHED
					BEGIN
						CONTROL txt_help1 AS TEXT
							POSITION 13 5 786 24
							ATTACH ALL
							NOBORDER
							DATATYPE CHAR
					END
					CONTROL AS BUTTON
						POSITION 671 217 134 34
						ATTACH TOP RIGHT
						LABEL "Reset Groups"
						COMMAND CmdResetAgregados
				END
			END
		END
	end
END
objects begin
	gChBackup AS Char
end
CODE CLASS frm BEGIN
//{{CODEBEGIN
On Open begin
    Module.PutEnv("HIDECOLUMNIFNODETAIL","YES");
    CargaLista(lstStrings);

end  
Private CargaLista ( pList as SimpleFormControl)
    Objects Begin
       
        icon as integer default 0
        texto as char
        valor as decimal
        InstruSelectString as char
    End
Begin
    
    pList.Reset();    
    
    pList.AlternateBackColor( Rgb(207, 247, 254) , Rgb(254, 247, 224), 2);
    pList.SetLockColumns(1);
   
 
   
    
    pList.IconFile = "listaiconosgif";



    
    pList.FieldSeparator = "|";
    
    FormatListColumnChar(1, 15, pList); //State
    FormatListColumnChar(2, 40, pList); //Customer
    FormatListColumnDate(3, 15, pList);  //Date

      
    FormatListColumnDate(4, 15, pList);  //Date

    FormatListColumnChar(5, 15, pList);  //Status

    
    FormatListColumnNumeric(6,10, 0, pList); //Número de la linea
    pList.SetColumnVisible(6, FALSE);
   
    FormatListColumnNumeric(7, 10,0, pList); //Cod item
    FormatListColumnChar(8, 15, pList);  //Descripcion item

    FormatListColumnNumeric(9, 15 ,0, pList); //Cantidad
    FormatListColumnNumeric(10, 15,0, pList); //Descuento
    FormatListColumnNumeric(11, 20, 2, pList); //Precio     
    pList.SetColumnMask(11,4);


    InstruSelect = "select customers.state, customers.company , order_date, pay_date , order_status , "+
                    " line_nbr, orderlines.item, items.description,  quantity, discount, price " +
                    
                    " from orders, orderlines, items , customers"+
                        " where orders.order_nbr = orderlines.order_nbr and orderlines.item = items.item " +
                        " and orderlines.supplier = items.supplier and orders.customer = customers.customer order by line_nbr";

    

    LoadString( pList, InstruSelect );
    
End

Private LoadString(pList as SimpleFormControl, pInstruSelect as char)
Objects begin
    c as SqlCursor
    texto as Char
    customer line_nbr as integer 
    item  quantity discount as smallint
    order_date pay_date as date
    order_status   description as char
    price price2 price3 as decimal (12,2)
    company state as char
    i as integer default 0
    j as integer default 0
    lChIndice as char
    InstruSelectString as char
end
begin

        c.Prepare(pInstruSelect);
    
        c.Open;
        
        While c.Fetch(state, company , order_date, pay_date , order_status , line_nbr, item, 
                    description,  quantity, discount, price ).Found
        Do begin

            texto = state + "|" +company +"|"+ order_date.char +"|"+ pay_date.char +"|"+ order_status +"|"+ line_nbr.Using(2) +"|"+ 
                    item.Using(5) +"|"+ description +"|"+  quantity +"|"+ discount +"|"+ 
                price.Using(12) ;
 
            j = pList.AddString(texto ,0);                        
            ++i;
            pList.SetListInfo(j, j);
        end
        c.Close.Free;
 
end


Private FormatListColumnChar(pNumColumn as smallint, pLengthChar as smallint, pList as SimpleFormControl)
objects begin
end
Begin
    pList.SetColumnType(pNumColumn,0);
    pList.SetColumnAlign(pNumColumn,0);
    pList.SetColumnChars(pNumColumn,pLengthChar);
end

Private FormatListColumnNumeric(pNumColumn as smallint, pLengthColumns as smallint, pNumDec as smallint , pList as SimpleFormControl)
Objects begin
end
begin
    pList.SetColumnType(pNumColumn ,1); //Numeric
    if pNumDec > 0 then pList.SetColumnNumDec(pNumColumn, pNumDec); //DEcimales
    pList.SetColumnAlign(pNumColumn,2);//Izda
    pList.SetColumnChars(pNumColumn,pLengthColumns);
end

Private FormatListColumnDate(pNumColumn as smallint, pLengthColumns as smallint, pList as SimpleFormControl)
Objects begin
end
begin
    pList.SetColumnType(pNumColumn ,2); //Date
    pList.SetColumnAlign(pNumColumn,2);//Dcha
    pList.SetColumnChars(pNumColumn,pLengthColumns);

end
    

On Command CmdAgruparAgregados

begin
    txt_help1.Text = "Drag and drop columns to be groupped";
    lstStrings.ShowMultiColumnGroupDlg(TRUE, 62,60 , TRUE, "Num", NULL, "Mínimo", "Suma", "Media");
end

On Command CmdVistaClasicaString
begin
    lstStrings.RemoveListGroupTreeView();
end


On Command CmdOrdenarColString
objects begin

end
begin
    txt_help1.Text = "Drag and drop columns to be ordered";
    Yield;
    lstStrings.ShowMultiColumnSortDlg(TRUE, TRUE);
end


On Command CmdAgruparAgregadosString 

begin
    txt_help1.Text = "Drag and drop columns to be groupped";
    lstStrings.ShowListAsMultiColumnGroup("GROUPBY:1,-2;AGGREGATES:11=SUM,11=MIN,9=COUNT;SHOWDETAILLINES:YES;ONLYORDERBY:7", 62,60 , TRUE, "Cuantos %p", NULL, "Mínimo %p", 
                        "Suma en: %p", "Valor medio: %p");
end

On Command CmdResetAgregados 

begin
    lstStrings.ShowListAsMultiColumnGroup("", 1, 1, TRUE, "");
end

//{{CODEEND
END
CODE BEGIN
//{{CODEBEGIN
main
objects
begin
    of as frm
    i dafaultRGB as integer
end
begin
    PutEnv("DRAWLISTLINES", "TRUE");
    
    PutEnv("DBPATH", ProjectDir());
    Sql.Connect("stock");
    
    of.Run();
    Sql.Disconnect();
end
//{{CODEEND
END

